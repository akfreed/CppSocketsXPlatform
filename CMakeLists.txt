cmake_minimum_required(VERSION 3.15)

# C++11
set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

option(VS_COMPAT_16_09 "If using VS 16.9 or older, use this option for warnings to be setup correctly.")

# Solution
project (CppSocketsXPlat)

# Warning flags
if(MSVC)
    if(NOT ${CMAKE_GENERATOR} STREQUAL "NMake Makefiles")
        add_compile_options(/MP)
    endif()
    set(WARNING_FLAGS /W4 /WX)
    if(${CMAKE_VERSION} VERSION_LESS 3.21)
        if(${VS_COMPAT_16_09})
            # In VS 16.9 and older, the "external" flag was experimental
            list(APPEND WARNING_FLAGS /experimental:external)
        endif()
        # Causes an unfortunate warning due to a missing flag for the new 16.10 external headers flag. Fixed in CMake 3.21
        list(APPEND WARNING_FLAGS /external:I ${CMAKE_SOURCE_DIR}/thirdparty/)
        list(APPEND WARNING_FLAGS /external:W0)
    endif()
elseif(CMAKE_COMPILER_IS_GNUCC OR CMAKE_COMPILER_IS_GNUCXX)
    # Update if necessary
    set(WARNING_FLAGS -Wall -Wextra -pedantic -Wconversion -Werror)
endif()

# Needed for clang-tidy
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)


# Dependencies
find_package(Threads REQUIRED)
add_subdirectory(thirdparty/)

# Projects
add_subdirectory(StrapperNet)
add_subdirectory(EchoServers)
add_subdirectory(UnitTest)


# clang-tidy needs a compile_commands.json file.
# Put a symlink in the source tree so clang-tidy can find it without needing to know the build directory.
# If this wasn't done, pre-commit, which calls clang-tidy, would need to pass the build path to clang-tidy as an argument.
# However pre-commit only supports hard-coded arguments, and there is no way to know the build path in advance.
#
# CMAKE_EXPORT_COMPILE_COMMANDS is supported by Make, NMake, and Ninja.
# MSVC users who need to use clang-tidy will need to do this:
#     Open Develop Command Prompt for VS 2019 as an Administrator.
#     create a dir for NMake build e.g. mkdir build_nmake && cd build_nmake
#     cmake .. -G "NMake Makefiles"
# This must be done before running clang-tidy if the cmake config has been re-run for Visual Studio.
file(REMOVE ${CMAKE_SOURCE_DIR}/compile_commands.json)
if(CMAKE_GENERATOR STREQUAL "Ninja" OR CMAKE_GENERATOR STREQUAL "Unix Makefiles" OR CMAKE_GENERATOR STREQUAL "NMake Makefiles")
    # The compile_commands.json file is generated by cmake, but not until later in the generation step.
    # Create a symlink / shortcut in the source tree that will point to the future file.
    # If the file doesn't exist, create a dummy file that will be overwritten when cmake completes.
    if(NOT EXISTS ${CMAKE_BINARY_DIR}/compile_commands.json)
        file(WRITE ${CMAKE_BINARY_DIR}/compile_commands.json "")
    endif()
    # Creating a shortcut needs admin privileges on Windows. If this fails (due to insufficient privileges), clang-tidy will fail
    execute_process(COMMAND ${CMAKE_COMMAND} -E create_symlink ${CMAKE_BINARY_DIR}/compile_commands.json ${CMAKE_SOURCE_DIR}/compile_commands.json)
endif()

# Set Unit Tests as the start-up project
set_property(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT UnitTest)
